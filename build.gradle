buildscript {
    ext {
        dependencyManagementVersion = '1.0.11.RELEASE'
        springBootVersion = '2.5.0'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    group = 'digital.patron'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {

        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.slf4j:slf4j-api'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    }

    sourceCompatibility = '11'
    targetCompatibility = '11'
    compileJava.options.encoding = 'UTF-8'

    test {
        useJUnitPlatform()
    }
}
project(':patron-common') {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }

    dependencies {
    }
}
project(':patron-admin'){
    dependencies {
        implementation project(':patron-common')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }
}

project(':patron-batch'){
    dependencies {
        implementation project(':patron-common')
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
        runtimeOnly 'com.h2database:h2'
        testImplementation 'org.springframework.batch:spring-batch-test'
    }

}

project(':patron-web'){
    dependencies {
        implementation project(':patron-common')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }
}
